// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    Inc16(in=oldVal, out=newVal);
    
    Mux16(a=false, b=newVal, sel=inc, out=incRes);
    Mux16(a=incRes, b=in, sel=load, out=loadRes);
    Mux16(a=loadRes, b=false, sel=reset, out=resetRes);

    Or(a=load, b=reset, out=ora);
    Or(a=ora, b=inc, out=changes);

    // If we didn't have any changes, load is 0 and therefore we return oldVal.
    // Otherwise, we update to resetRes, which has the updated value after inc, load and reset.
    Register(in=resetRes, load=changes, out=oldVal, out=out);
}
